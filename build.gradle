apply plugin: 'java'
apply plugin: 'groovy'
apply plugin: 'maven'
apply plugin: 'idea'

def defaultVersion = '4.1-SNAPSHOT'
def BUILD_VERSION = 'buildVersion'

def artifactVersion = rootProject.hasProperty(BUILD_VERSION) ? rootProject[BUILD_VERSION] : defaultVersion
def isSnapshot = artifactVersion.endsWith('-SNAPSHOT')

group = 'com.haulmont.gradle'
version = artifactVersion

ext.repositoryUrl = 'http://repository.haulmont.com:8587/nexus/content'
ext.repositoryUser = System.getenv('HAULMONT_REPOSITORY_USER')
ext.repositoryPassword = System.getenv('HAULMONT_REPOSITORY_PASSWORD')

repositories {
    mavenLocal()
    maven {
        credentials {
            username repositoryUser
            password repositoryPassword
        }
        url "http://repository.haulmont.com:8587/nexus/content/groups/work"
    }
}

configurations {
    deployerJars
}

dependencies {
    compile(group: 'org.apache.openjpa', name: 'openjpa', version: '2.2.1')
    compile(group: 'dom4j', name: 'dom4j', version: '1.6.1')
    compile(group: 'commons-io', name: 'commons-io', version: '1.4')
    compile(group: 'org.codehaus.gpars', name: 'gpars', version: '1.0.0')
    compile(group: 'com.yahoo.platform.yui', name: 'yuicompressor', version: '2.4.6')
    compile(group: 'com.carrotsearch', name: 'smartsprites', version: '0.2.8')
    compile(gradleApi())
    groovy(localGroovy())
    deployerJars(group: 'org.apache.maven.wagon', name: 'wagon-http', version: '1.0-beta-2')
}

task sourceJar(type: Jar) {
    from sourceSets.main.groovy
    from sourceSets.main.resources
    classifier = 'sources'
} 

artifacts {
    archives sourceJar
}

uploadArchives.configure {
    repositories.mavenDeployer {
        name = 'httpDeployer'
        configuration = configurations.deployerJars
        repository(url: "$repositoryUrl/repositories/" + (isSnapshot ? 'snapshots' : 'releases')) {
            authentication(userName: repositoryUser, password: repositoryPassword)
        }
    }
}
